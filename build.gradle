buildscript {
    repositories {
        jcenter()
        maven { url "http://repo.spring.io/snapshot" }
        maven { url "http://repo.spring.io/milestone" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:1.5.2.RELEASE")
    }
}

plugins {
    id "org.flywaydb.flyway" version "4.1.2"
}

flyway {
    locations = [ 'db/migrations' ]
}

group 'com.thanethomson.demos'
version '0.1.0'

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'idea'

sourceCompatibility = 1.8

repositories {
    jcenter()
    maven { url "http://repo.spring.io/snapshot" }
    maven { url "http://repo.spring.io/milestone" }
}

configurations {
    compile.exclude module: "spring-boot-starter-tomcat"
}

dependencies {
    // Spring Boot dependencies
    compile("org.springframework.boot:spring-boot-starter-web")
    compile("org.springframework.boot:spring-boot-starter-jetty")
    compile("org.springframework.boot:spring-boot-starter-data-jpa")
    compile("org.springframework.boot:spring-boot-starter-data-rest")
    compile("org.springframework.vault:spring-vault-core:1.0.0.RC1")

    // Database-related dependencies
    compile("com.zaxxer:HikariCP:2.6.1")
    compile("org.postgresql:postgresql:42.0.0")

    // Testing dependencies
    testCompile("org.springframework.boot:spring-boot-starter-test")
}

bootRun {
    systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
}

test {
    systemProperty 'spring.profiles.active', System.properties['spring.profiles.active']
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.4'
    distributionUrl = "https://services.gradle.org/distributions/gradle-$gradleVersion-all.zip"
}
